apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 28,

      'androidTools': '26.3.0',
      'kotlin': '1.3.21',

      'release': '8.8.1',
  ]

  ext.deps = [
      android: [
          'runtime': 'com.google.android:android:4.1.1.4',
          'gradlePlugin': "com.android.tools.build:gradle:3.3.1",
      ],
      'androidx': [
          'core': "androidx.core:core:1.0.0",
          'viewpager': "androidx.viewpager:viewpager:1.0.0",
          'annotations': "androidx.annotation:annotation:1.0.0",
          'test': [
              'runner': 'androidx.test:runner:1.1.0',
              'rules': 'androidx.test:rules:1.1.0',
          ],
      ],
      'lint': [
          'core': "com.android.tools.lint:lint:${versions.androidTools}",
          'api': "com.android.tools.lint:lint-api:${versions.androidTools}",
          'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
          'tests': "com.android.tools.lint:lint-tests:${versions.androidTools}",
      ],
      javapoet: 'com.squareup:javapoet:1.10.0',
      junit: 'junit:junit:4.12',
      truth: 'com.google.truth:truth:0.42',
      compiletesting: 'com.google.testing.compile:compile-testing:0.15',
      'auto': [
          'service': 'com.google.auto.service:auto-service:1.0-rc4',
          'common': 'com.google.auto:auto-common:0.10',
      ],
      'guava': 'com.google.guava:guava:24.0-jre',
      'release': [
          'runtime': "com.jakewharton:butterknife:${versions.release}",
          'compiler': "com.jakewharton:butterknife-compiler:${versions.release}"
      ],
      'kotlin': [
          'stdLibJdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ]
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.7'
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  apply plugin: 'net.ltgt.errorprone'

  dependencies {
    //noinspection GradleDynamicVersion
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
    errorprone 'com.google.errorprone:error_prone_core:2.3.2'
    errorprone 'com.uber.nullaway:nullaway:0.6.4'
  }

  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      option("NullAway:AnnotatedPackages", "butterknife")
      check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
    }
  }

  if (project.name != 'butterknife-gradle-plugin') {
    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
      configFile rootProject.file('checkstyle.xml')
      source 'src/main/java'
      ignoreFailures false
      showViolations true
      include '**/*.java'

      classpath = files()
    }

    afterEvaluate {
      if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
      }
    }
  }
}
