buildscript {
  ext.versions = [
      'minSdk': 9,
      'compileSdk': 25,
      'buildTools': '25.0.2',

      'supportLibrary': '25.1.0',
      'androidPlugin': '2.2.0',
      'androidTools': '25.2.0',
      'kotlin': '1.1.2-3',

      'release': '8.4.0',
  ]

  ext.deps = [
      android: [
          'runtime': 'com.google.android:android:4.1.1.4',
          'gradlePlugin': "com.android.tools.build:gradle:${versions.androidPlugin}",
      ],
      'support': [
          'compat': "com.android.support:support-compat:${versions.supportLibrary}",
          'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
          'test': [
              'runner': 'com.android.support.test:runner:0.5',
          ],
      ],
      'lint': [
          'core': "com.android.tools.lint:lint:${versions.androidTools}",
          'api': "com.android.tools.lint:lint-api:${versions.androidTools}",
          'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
          'tests': "com.android.tools.lint:lint-tests:${versions.androidTools}",
      ],
      javapoet: 'com.squareup:javapoet:1.8.0',
      javaparser: 'com.github.javaparser:javaparser-core:2.4.0',
      junit: 'junit:junit:4.12',
      truth: 'com.google.truth:truth:0.28',
      robolectric: 'org.robolectric:robolectric:3.1.2',
      openglApi: 'org.khronos:opengl-api:gl1.1-android-2.1_r1',
      compiletesting: 'com.google.testing.compile:compile-testing:0.9',
      'auto': [
          'service': 'com.google.auto.service:auto-service:1.0-rc2',
          'common': 'com.google.auto:auto-common:0.6',
      ],
      'release': [
          'runtime': "com.jakewharton:butterknife:${versions.release}",
          'compiler': "com.jakewharton:butterknife-compiler:${versions.release}"
      ],
      'kotlin': [
          'stdLibJre8': "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}",
      ]
  ]

  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath deps.android.gradlePlugin
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'gradle.plugin.com.kageiit:lintrules:1.1.2'
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  if (!project.name.equals('butterknife-gradle-plugin')) {
    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
      configFile rootProject.file('checkstyle.xml')
      source 'src/main/java'
      ignoreFailures false
      showViolations true
      include '**/*.java'

      classpath = files()
    }

    afterEvaluate {
      if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
      }
    }
  }
}
