apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 27,

      'supportLibrary': '27.0.2',
      'androidPlugin': '3.0.1',
      'androidTools': '26.0.1',
      'kotlin': '1.2.10',

      'release': '8.8.1',
  ]

  ext.deps = [
      android: [
          'runtime': 'com.google.android:android:4.1.1.4',
          'gradlePlugin': "com.android.tools.build:gradle:${versions.androidPlugin}",
      ],
      'support': [
          'compat': "com.android.support:support-compat:${versions.supportLibrary}",
          'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
          'test': [
              'runner': 'com.android.support.test:runner:1.0.1',
          ],
      ],
      'lint': [
          'core': "com.android.tools.lint:lint:${versions.androidTools}",
          'api': "com.android.tools.lint:lint-api:${versions.androidTools}",
          'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
          'tests': "com.android.tools.lint:lint-tests:${versions.androidTools}",
      ],
      javapoet: 'com.squareup:javapoet:1.9.0',
      javaparser: 'com.github.javaparser:javaparser-core:3.5.7',
      junit: 'junit:junit:4.12',
      truth: 'com.google.truth:truth:0.37',
      robolectric: 'org.robolectric:robolectric:3.6.1',
      compiletesting: 'com.google.testing.compile:compile-testing:0.15',
      'auto': [
          'service': 'com.google.auto.service:auto-service:1.0-rc4',
          'common': 'com.google.auto:auto-common:0.9',
      ],
      'guava': 'com.google.guava:guava:23.6-jre',
      'release': [
          'runtime': "com.jakewharton:butterknife:${versions.release}",
          'compiler': "com.jakewharton:butterknife-compiler:${versions.release}"
      ],
      'kotlin': [
          'stdLibJre8': "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}",
      ]
  ]

  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  dependencies {
    classpath deps.android.gradlePlugin
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  if (!project.name.equals('butterknife-gradle-plugin')) {
    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
      configFile rootProject.file('checkstyle.xml')
      source 'src/main/java'
      ignoreFailures false
      showViolations true
      include '**/*.java'

      classpath = files()
    }

    afterEvaluate {
      if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
      }
    }
  }
}
