apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
  fixtureClasspath
}
// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
def metadata = tasks.getByName('pluginUnderTestMetadata')
metadata.setPluginClasspath(metadata.getPluginClasspath() + configurations.fixtureClasspath)

dependencies {
  compileOnly gradleApi()

  implementation deps.android.gradlePlugin
  implementation deps.javaparser
  implementation deps.javapoet
  implementation deps.kotlin.stdLibJre8

  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.support.annotations
  testImplementation deps.compiletesting
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
