apply plugin: 'com.android.library'

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    minSdkVersion versions.minSdk

    consumerProguardFiles 'proguard-rules.txt'

    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

    javaCompileOptions {
      annotationProcessorOptions {
        includeCompileClasspath = true
      }
    }
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
    checkReleaseBuilds false
  }
}

dependencies {
  api project(':butterknife-annotations')
  api deps.support.compat

  lintChecks project(':butterknife-lint')

  androidTestImplementation deps.junit
  androidTestImplementation deps.truth
  androidTestImplementation deps.support.test.runner
  androidTestAnnotationProcessor project(':butterknife-compiler')

  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.compiletesting
  testImplementation files(getRuntimeJar())
  testImplementation files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
  testImplementation project(':butterknife-compiler')
  testCompileOnly deps.javapoet
}

project.tasks.all {
  if (it.name =~ /AndroidTestJavaWithJavac$/) {
    it.options.with {
      setCompilerArgs(getCompilerArgs() + [
              // runner-1.0.1.aar\1fdddc98585f7e7ee30d9c2c44d0ee7f\jars\classes.jar
              // (android/support/test/InstrumentationRegistry.class):
              // warning: [classfile] MethodParameters attribute
              // introduced in version 52.0 class files is ignored in version 51.0 class files
              '-Xlint:-classfile'
      ])
    }
  }
}

static def getRuntimeJar() {
  final javaBase = new File(System.properties.'java.home').canonicalFile
  [ 'lib/rt.jar', 'jre/lib/rt.jar' ]
      .collect { new File(javaBase, it) }
      .grep { it.exists() }
      .find()
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
